plugins {
    id 'com.google.protobuf' version '0.9.4'
}

dependencies {
    implementation project(':horizon-core')
    implementation project(':horizon-web')

    // gRPC dependencies
    implementation 'io.grpc:grpc-netty-shaded:1.58.0'
    implementation 'io.grpc:grpc-protobuf:1.58.0'
    implementation 'io.grpc:grpc-stub:1.58.0'
    implementation 'com.google.protobuf:protobuf-java:3.24.4'
    implementation 'com.google.protobuf:protobuf-java-util:3.24.4'

    // For gRPC code generation
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

// Protocol Buffer configuration
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.4'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.58.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Configure the main class for running the demo
tasks.register('run', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'horizon.demo.DemoApplication'
}
